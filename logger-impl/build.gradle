group 'com.altran.igjava'
version '1.0-SNAPSHOT'

apply plugin: 'java'

ext.moduleName='com.altran.igjava.logger.impl'

dependencies {
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.9.1'
    runtime group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.9.1'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.10.0'
}

compileTestJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'junit',
                '--add-reads', "$moduleName=junit",
                '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath,
        ]
        classpath = files()
    }
}

test {
    inputs.property("moduleName",moduleName)
    doFirst {
        jvmArgs = [
                '--module-path',classpath.asPath,
                '--add-modules', 'ALL-MODULE-PATH',
                '--add-reads', "$moduleName=junit",
                '--patch-module', "$moduleName=" +files(sourceSets.test.java.outputDir).asPath
        ]

        classpath = files()
    }
}

jar {
    inputs.property("moduleName",moduleName)
    manifest {
        attributes('Automatic-Module-Name': moduleName)
    }
}