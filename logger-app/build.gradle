import java.util.regex.Matcher

group 'com.altran.igjava'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'application'

repositories {
    mavenCentral()
}

dependencies {
    runtimeOnly project(':logger-impl')
}

ext.moduleName = 'com.altran.igjava.logger.app'

mainClassName = "$moduleName/com.altran.igjava.logger.app.Main"

compileJava {
    inputs.property("moduleName",moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path',classpath.asPath,
        ]
        classpath = files()
    }
}

run {
    inputs.property("moduleName", moduleName)
    doFirst {
        jvmArgs = [
                '--module-path', classpath.asPath,
                '--module', mainClassName
        ]
        classpath = files()
    }
}

startScripts {
    inputs.property("moduleName",moduleName)
    doFirst {
        classpath =files()
        defaultJvmOpts = [
                '--module-path','APP_HOME_LIBS',
                '--module', mainClassName
        ]
    }
    doLast {
        def bashFile = new File(outputDir, applicationName)
        String bashContent = bashFile.text
        bashFile.text = bashContent.replaceFirst('APP_HOME_LIBS', Matcher.quoteReplacement('$APP_HOME/lib'))

        def batFile = new File(outputDir,applicationName +".bat")
        String batContent = batFile.text
        batFile.text = batContent.replaceFirst('APP_HOME_LIBS',Matcher.quoteReplacement('%APP_HOME%\\lib'))

    }
}


javadoc {
   options.addStringOption('--add-modules', 'ALL-MODULE=PATH')
}